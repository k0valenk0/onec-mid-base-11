#Область ПрограммныйИнтерфейс

// Программно дополняет указанную форму дополнительными элементами и командами.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма, которую необходимо дополнить элементами управления
//
Процедура ДополнитьФорму(Форма) Экспорт
	
	//Начало.Коваленко: добавляем элементы и команды на открываемую форму
	ИмяФормы = Форма.ИмяФормы;
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
		ДобавитьГруппаСкидкаПересчитать(Форма);
		ДобавитьПолеСогласованнаяCкидкаВГруппаСкидкаПересчитать(Форма);
		ДобавитьКомандаПересчитатьТаблицуВГруппаСкидкаПересчитать(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередОснование(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
	КонецЕсли;
	//Конец.Коваленко
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
	
	//Начало.Коваленко: Добавляем элемент в коллекцию элементов, выводим на форму в определенное место
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Д_КонтактноеЛицо";
	//Конец.Коваленко
	
КонецПроцедуры

Процедура ВставитьПолеКонтактноеЛицоНаФормуПередОснование(Форма)
	
	//Начало.Коваленко: Добавляем элемент в коллекцию элементов, выводим на форму в определенное место
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , форма.Элементы.Основание);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Д_КонтактноеЛицо";
	//Конец.Коваленко

КонецПроцедуры

Процедура ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма)
	
	//Начало.Коваленко: Добавляем элемент в коллекцию элементов, выводим на форму в определенное место
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Д_КонтактноеЛицо";
	//Конец.Коваленко
	
КонецПроцедуры

Процедура ДобавитьГруппаСкидкаПересчитать(Форма)
	
	//Начало.Коваленко: Добавляем элемент в коллекцию элементов, выводим на форму в определенное место
	ГруппаСкидкаПересчитать = Форма.Элементы.Добавить("ГруппаСкидкаПересчитать", Тип("ГруппаФормы"), форма.Элементы.ГруппаШапкаЛево);
	ГруппаСкидкаПересчитать.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСкидкаПересчитать.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаСкидкаПересчитать.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	ГруппаСкидкаПересчитать.ОтображатьЗаголовок = Ложь;
	ГруппаСкидкаПересчитать.РастягиватьПоГоризонтали = Ложь;
	//Конец.Коваленко
	
КонецПроцедуры

Процедура ДобавитьПолеСогласованнаяCкидкаВГруппаСкидкаПересчитать(Форма)
	
	//Начало.Коваленко: Добавляем элемент в коллекцию элементов, выводим на форму в определенное место 
	ПолеВвода = Форма.Элементы.Добавить("СогласованнаяCкидка", Тип("ПолеФормы"), форма.Элементы.ГруппаСкидкаПересчитать);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Д_СогласованнаяСкидка";
	ПолеВвода.УстановитьДействие("ПриИзменении", "Д_СогласованнаяCкидкаПриИзменении")
	//Конец.Коваленко
	
КонецПроцедуры

Процедура ДобавитьКомандаПересчитатьТаблицуВГруппаСкидкаПересчитать(Форма)
	
	//Начало.Коваленко: Добавляем команду в коллекцию команд, выводим на форму в определенное место
	КомандаПересчитатьТаблицу = Форма.Команды.Добавить("ПересчитатьТаблицу");
	КомандаПересчитатьТаблицу.Заголовок = "Пересчитать Таблицу";
	КомандаПересчитатьТаблицу.Действие = "ПересчитатьТаблицу";
	
	КнопкаКоманды = Форма.Элементы.Добавить("ПересчитатьТаблицу", Тип("КнопкаФормы"), форма.Элементы.ГруппаСкидкаПересчитать);
	КнопкаКоманды.ИмяКоманды = "ПересчитатьТаблицу";
	КнопкаКоманды.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	КнопкаКоманды.Отображение = ОтображениеКнопки.КартинкаИТекст;
	КнопкаКоманды.Картинка = БиблиотекаКартинок.Перечитать;
	//Конец.Коваленко
	
КонецПроцедуры

#КонецОбласти